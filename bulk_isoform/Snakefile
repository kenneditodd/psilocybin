configfile: "refs/config.json"


# CONFIG VARIABLES
#--------------------------------------------------------------------------------
rawReadsDir = config["DIRECTORIES"]["rawReads"]
trimmedReadsDir = config["DIRECTORIES"]["trimmedReads"]
rawQCDir = config["DIRECTORIES"]["rawQC"]
trimmedQCDir = config["DIRECTORIES"]["trimmedQC"]


# ENVIRONMENT VARIABLES
#-------------------------------------------------------------------------------
import os
from dotenv import load_dotenv

# Load the .env file
load_dotenv("refs/.env")

# Get paths from environment variables
gtf_file = os.getenv("MMUSCULUS_GTF")
genome_fa_file = os.getenv("MMUSCULUS_FA")
projectDir = os.getenv("PROJECT_DIR")


# RULE ALL
#-------------------------------------------------------------------------------
rule all:
    input:
        projectDir + "/refs/transcripts.fa",
        projectDir + "/refs/kallisto.idx",
        expand(trimmedReadsDir + "{sample}_trim_R1.fastq.gz", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        expand(trimmedReadsDir + "{sample}_trim_R2.fastq.gz", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        expand(rawQCDir + "{sample}_R1_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        expand(trimmedQCDir + "{sample}_trim_R1_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        rawQCDir + "multiqc_report.html",
        trimmedQCDir + "multiqc_report.html",
        expand(projectDir + "/kallisto/{sample}_kallisto/abundance.tsv", sample=config["SAMPLE_INFORMATION"]["allSamples"])


# CREATE TRANSCRIPTS
#-------------------------------------------------------------------------------
rule create_transcripts:
    input:
        gtf = gtf_file,
        genome = genome_fa_file
    output:
        transcripts = projectDir + "/refs/transcripts.fa"
    shell:
        """
        gffread {input.gtf} -g {input.genome} -w {output.transcripts}
        """
        
 
 # INDEX TRANSCRIPTS
#-------------------------------------------------------------------------------
rule index_transcripts:
    input:
        transcripts = projectDir + "/refs/transcripts.fa"
    output:
        index = projectDir + "/refs/kallisto.idx"
    params:
        threads = config["CLUSTER_INFORMATION"]["threads"]
    shell:
        """
        kallisto index --threads {params.threads} --index {output.index} {input.transcripts}
        """


# RAW FASTQC
#-------------------------------------------------------------------------------
rule raw_fastqc:
    input:
        R1 = lambda wildcards: rawReadsDir + config["SAMPLES"][wildcards.sample]["read1"] + ".fastq.gz",
        R2 = lambda wildcards: rawReadsDir + config["SAMPLES"][wildcards.sample]["read2"] + ".fastq.gz"
    output:
        qc1 = rawQCDir + "{sample}_R1_fastqc.zip",
        qc2 = rawQCDir + "{sample}_R2_fastqc.zip"
    params:
        threads = config["CLUSTER_INFORMATION"]["threads"]
    shell:
        """
        fastqc {input.R1} {input.R2} -o {rawQCDir} --threads {params.threads}
        """


# TRIM BBDUK
#-------------------------------------------------------------------------------
rule trim_bbduk:
    input:
        R1 = lambda wildcards: rawReadsDir + config["SAMPLES"][wildcards.sample]["read1"] + ".fastq.gz",
        R2 = lambda wildcards: rawReadsDir + config["SAMPLES"][wildcards.sample]["read2"] + ".fastq.gz"
    output:
        trimR1 = trimmedReadsDir + "{sample}_trim_R1.fastq.gz",
        trimR2 = trimmedReadsDir + "{sample}_trim_R2.fastq.gz"
    params:
        threads = config["CLUSTER_INFORMATION"]["threads"]
    shell:
        """
        bbduk.sh -Xmx3g in1={input.R1} in2={input.R2} out1={output.trimR1} out2={output.trimR2} ref=refs/adapters.fa ktrim=r k=23 mink=11 hdist=1 tpe tbo threads={params.threads} trimpolyg=1 trimpolya=1
        """


# TRIMMED FASTQC
#-------------------------------------------------------------------------------
rule trimmed_fastqc:
    input:
        R1 = trimmedReadsDir + "{sample}_trim_R1.fastq.gz",
        R2 = trimmedReadsDir + "{sample}_trim_R2.fastq.gz"
    output:
        qc1 = trimmedQCDir + "{sample}_trim_R1_fastqc.zip",
        qc2 = trimmedQCDir + "{sample}_trim_R2_fastqc.zip"
    params:
        threads = config["CLUSTER_INFORMATION"]["threads"]
    shell:
        """
        fastqc {input.R1} {input.R2} -o {trimmedQCDir} --threads {params.threads}
        """


# MULTIQC FOR RAW FASTQC
#-------------------------------------------------------------------------------
rule raw_multiqc:
    input:
        expand(rawQCDir + "{sample}_R1_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        expand(rawQCDir + "{sample}_R2_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"])
    output:
        html = rawQCDir + "multiqc_report.html"
    params:
        outdir = rawQCDir
    shell:
        """
        multiqc --interactive {rawQCDir} -o {params.outdir}
        """


# MULTIQC FOR TRIMMED FASTQC
#-------------------------------------------------------------------------------
rule trimmed_multiqc:
    input:
        expand(trimmedQCDir + "{sample}_trim_R1_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"]),
        expand(trimmedQCDir + "{sample}_trim_R2_fastqc.zip", sample=config["SAMPLE_INFORMATION"]["allSamples"])
    output:
        html = trimmedQCDir + "multiqc_report.html"
    params:
        outdir = trimmedQCDir
    shell:
        """
        multiqc --interactive {trimmedQCDir} -o {params.outdir}
        """


# KALLISTO QUANT
#-------------------------------------------------------------------------------
rule kallisto_quant:
    input:
        index = projectDir + "/refs/kallisto.idx",
        trimR1 = trimmedReadsDir + "{sample}_trim_R1.fastq.gz",
        trimR2 = trimmedReadsDir + "{sample}_trim_R2.fastq.gz"
    output:
        abundance = projectDir + "/kallisto/{sample}_kallisto/abundance.tsv"
    params:
      threads = config["CLUSTER_INFORMATION"]["threads"],
      outdir = projectDir + "/kallisto/{sample}_kallisto"
    shell:
        """
        kallisto quant --index {input.index} --output-dir {params.outdir} --bootstrap-samples 100 --threads {params.threads} --rf-stranded {input.trimR1} {input.trimR2}
        """
  
